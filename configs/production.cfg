# 这句必须在最前面！！！默认第一个是最初的产生式，方便增广
Program -> DeclarationList

# 0.1 变量声明内部（可变）
VariableDeclarationInner -> MUT IDENTIFIER

# 0.3 
Assignable -> IDENTIFIER

# 6.1 变量声明内部（不可变）
VariableDeclarationInner -> IDENTIFIER

# 0.2 类型
Type -> I32

# 6.2 借用与引用
Assignable -> MULT IDENTIFIER
Factor -> MULT Factor
Factor -> AMP MUT Factor
Factor -> AMP Factor
Type -> AMP MUT Type
Type -> AMP Type

# 8.1 数组类型
Type -> LBRACK Type SEMICOLON INTEGER_CONSTANT RBRACK
Factor -> LBRACK ArrayElementsList RBRACK
Factor -> LBRACK ArrayElements RBRACK
ArrayElementsList -> epsilon
ArrayElementsList -> Expression
ArrayElementsList -> Expression COMMA ArrayElementsList

# 8.2 数组元素
Assignable -> Primary LBRACK Expression RBRACK
IterableStructure -> Primary

# 1.1 基础程序
DeclarationList -> epsilon
DeclarationList -> Declaration DeclarationList

Declaration -> FunctionDeclaration

# 函数声明
FunctionDeclaration -> FunctionHeader StatementBlock

# 1.5 函数返回类型
FunctionHeader -> FN IDENTIFIER LPAREN ParameterList RPAREN
FunctionHeader -> FN IDENTIFIER LPAREN ParameterList RPAREN ARROW Type

# 1.4 形参列表
ParameterList -> epsilon
ParameterList -> Parameter
ParameterList -> Parameter COMMA ParameterList

Parameter -> VariableDeclarationInner COLON Type

# 1.2、1.3、2、3 语句
StatementBlock -> LBRACE StatementList RBRACE

StatementList -> epsilon
StatementList -> Statement StatementList

# 空语句
Statement -> SEMICOLON                  
Statement -> ReturnStatement
Statement -> LET VariableDeclarationInner COLON Type SEMICOLON
Statement -> LET VariableDeclarationInner SEMICOLON
Statement -> AssignmentStatement
Statement -> VariableDeclarationAssignmentStatement
Statement -> Expression SEMICOLON
Statement -> IfStatement
Statement -> WhileStatement
Statement -> ForStatement
Statement -> LoopStatement
Statement -> BREAK SEMICOLON
Statement -> CONTINUE SEMICOLON
# 循环表达式中的 break
Statement -> BREAK Expression SEMICOLON  

# 1.3 返回语句
ReturnStatement -> RETURN SEMICOLON
ReturnStatement -> RETURN Expression SEMICOLON

# 2.2 赋值语句
AssignmentStatement -> VariableDeclarationInner ASSIGN Expression SEMICOLON
AssignmentStatement -> Assignable ASSIGN Expression SEMICOLON

# 2.3 声明并赋值
VariableDeclarationAssignmentStatement -> LET VariableDeclarationInner ASSIGN Expression SEMICOLON
VariableDeclarationAssignmentStatement -> LET VariableDeclarationInner COLON Type ASSIGN Expression SEMICOLON

# 3.1–3.3 表达式
Expression -> AdditionExpression
Expression -> Expression ComparisonOperator AdditionExpression
Expression -> BlockExpression
Expression -> ConditionalExpression
Expression -> LoopExpression

AdditionExpression -> Term
AdditionExpression -> AdditionExpression AddOp Term

Term -> Factor
Term -> Term MulOp Factor

Factor -> Primary

Primary -> INTEGER_CONSTANT
Primary -> MINUS INTEGER_CONSTANT
# Primary -> IDENTIFIER
Primary -> Assignable
Primary -> LPAREN Expression RPAREN
Primary -> IDENTIFIER LPAREN ArgumentList RPAREN
Primary -> LBRACK ArrayElements RBRACK
Primary -> LPAREN TupleAssignmentInternal RPAREN

# 运算符
ComparisonOperator -> LT
ComparisonOperator -> LE
ComparisonOperator -> GT
ComparisonOperator -> GE
ComparisonOperator -> EQ
ComparisonOperator -> NEQ

AddOp -> PLUS
AddOp -> MINUS

MulOp -> MULT
MulOp -> DIV

ArgumentList -> epsilon
ArgumentList -> Expression
ArgumentList -> Expression COMMA ArgumentList

ArrayElements -> epsilon
ArrayElements -> Expression
ArrayElements -> Expression COMMA ArrayElements

# 4.1–4.2 if／else
IfStatement -> IF Expression StatementBlock ElsePart

ElsePart -> epsilon
ElsePart -> ELSE StatementBlock
ElsePart -> ELSE IF Expression StatementBlock ElsePart

# 5.1 while
WhileStatement -> WHILE Expression StatementBlock

# 5.2 for
ForStatement -> FOR VariableDeclarationInner IN IterableStructure StatementBlock
IterableStructure -> Expression DOTDOT Expression

# 5.3 loop
LoopStatement -> LOOP StatementBlock

# 7.1 块表达式
BlockExpression -> LBRACE BlockStmtList RBRACE
BlockStmtList -> Expression
BlockStmtList -> Statement BlockStmtList

# 7.2 函数表达式块作为函数体
FunctionDeclaration -> FunctionHeader BlockExpression

# 7.4 循环表达式
LoopExpression -> LOOP StatementBlock

# 7.3 条件表达式
ConditionalExpression -> IF Expression BlockExpression ELSE BlockExpression

# 9.1 元组类型
Type -> LPAREN TupleTypeInternal RPAREN
TupleTypeInternal -> epsilon
# TupleTypeInternal -> Type
TupleTypeInternal -> Type COMMA TypeList
TypeList -> epsilon
TypeList -> Type
TypeList -> Type COMMA TypeList
TupleAssignmentInternal -> epsilon
# TupleAssignmentInternal -> Expression
TupleAssignmentInternal -> Expression COMMA TupleAssignmentList
TupleAssignmentList -> epsilon
TupleAssignmentList -> Expression
TupleAssignmentList -> Expression COMMA TupleAssignmentList

# 9.2 元组元素访问
Assignable -> Primary DOT INTEGER_CONSTANT
